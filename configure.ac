AC_PREREQ(2.60)
AC_INIT([libgepub],[0.5],[danigm@wadobo.com])
AC_CONFIG_AUX_DIR([build])
AM_INIT_AUTOMAKE([1.10 dist-bzip2])
AM_SILENT_RULES([yes])

GNOME_COMMON_INIT

# automake 1.12 seems to require this, but automake 1.11 doesn't recognize it
m4_pattern_allow([AM_PROG_AR])
AM_PROG_AR

AC_PROG_CC
# Compiling sources with per-target flags requires AM_PROG_CC_C_O
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_LIBTOOL

AC_CHECK_PROG(CARGO, [cargo], [yes], [no])
AS_IF(test x$CARGO = xno,
    AC_MSG_ERROR([cargo is required.  Please install the Rust toolchain from https://www.rust-lang.org/])
)
AC_CHECK_PROG(RUSTC, [rustc], [yes], [no])
AS_IF(test x$RUSTC = xno,
    AC_MSG_ERROR([rustc is required.  Please install the Rust toolchain from https://www.rust-lang.org/])
)

AC_ARG_ENABLE(debug,
              AC_HELP_STRING([--enable-debug],
                             [Build Rust code with debugging information [default=no]]),
              [debug_release=$enableval],
	      [debug_release=no])

AC_MSG_CHECKING(whether to build Rust code with debugging information)
if test "x$debug_release" = "xyes" ; then
	AC_MSG_RESULT(yes)
	RUST_TARGET_SUBDIR=debug
else
	AC_MSG_RESULT(no)
	RUST_TARGET_SUBDIR=release
fi
AM_CONDITIONAL([DEBUG_RELEASE], [test "x$debug_release" = "xyes"])

AC_SUBST([RUST_TARGET_SUBDIR])


PKG_CHECK_MODULES(GEPUB,
                  webkit2gtk-4.0
                  libsoup-2.4
                  glib-2.0
                  gobject-2.0
                  gio-2.0
                  libxml-2.0
                  libarchive)

AC_SUBST(GEPUB_CFLAGS)
AC_SUBST(GEPUB_LIBS)

PKG_CHECK_MODULES(GEPUB_TESTS,
                  webkit2gtk-4.0
                  libsoup-2.4
                  glib-2.0
                  gobject-2.0
                  gtk+-3.0
                  gio-2.0
                  libxml-2.0
                  libarchive)

AC_SUBST(GEPUB_TESTS_CFLAGS)
AC_SUBST(GEPUB_TESTS_LIBS)

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_FILES([
    Makefile
    libgepub.pc
    libgepub/Makefile
    tests/Makefile
])

# introspection
GOBJECT_INTROSPECTION_CHECK([1.30.0])

AC_OUTPUT
